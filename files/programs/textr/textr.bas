200 "TEXTR"
210 "A": CLEAR : WAIT 59: PRINT "TEXTR V.1.13": BEEP 1
220 "MN":I=0: INPUT "M-ENU;Q-UIT:";C$
230 IF C$="Q" GOSUB "TQ"
240 IF C$="T" GOSUB "TT"
250 IF C$="V" GOSUB "TP"
260 IF C$="P" THEN PRINT =LPRINT : GOSUB "TP"
270 IF C$="L" GOSUB "MM": INPUT #"TEXTR";A$(*): GOTO "MN"
280 IF C$="S" THEN PRINT #"TEXTR";A$(*): GOTO "MN"
290 PRINT "Q-UIT;T-EXT"
300 PRINT "V-IEW;P-RINT"
310 PRINT "L-OAD;S-AVE"
320 GOTO "MN"
330 "TT": INPUT "N-EW;OLD:";C$: IF C$="N" GOSUB "MM"
340 "IN":I=I+1
350 INPUT "T:";A$(I)
360 IF N=I OR A$(I)="" THEN RETURN 
370 GOTO "IN"
380 RETURN 
390 "TP": INPUT "1-24;0-AUTO:";H:Z=1
400 "PR":I=I+1
410 IF A$(I)="" THEN PRINT =PRINT : RETURN 
420 X=LEN (A$(I)): IF H=0 THEN LET X=-1
430 IF X<H OR H=0 THEN PRINT A$(I)
440 "P1": IF X>H OR X=H THEN PRINT MID$ (A$(I),Z,H):Z=Z+H: IF X>Z OR X=Z THEN "P1"
450 IF I=N THEN PRINT =PRINT : RETURN 
460 Z=1: GOTO "PR"
470 RETURN 
480 "MM": CLEAR :N=10: DIM A$(N)*80: RETURN 
490 "TQ": INPUT "QUIT(Y/N)?:";C$: IF C$="Y" THEN CLEAR : WAIT : END 
500 RETURN 
510 END 